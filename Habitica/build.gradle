apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.5'
        classpath 'me.tatarka:gradle-retrolambda:3.3.1'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://maven.fabric.io/public' }

    // Material View Pager
    maven { url "http://dl.bintray.com/florent37/maven" }

    // Markdown
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }

    maven { url "https://jitpack.io" }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //Networking
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    //REST API handling
    compile('com.squareup.retrofit2:retrofit:2.1.0') {
        exclude module: 'okhttp'
    }
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'

    //Crash Logging
    releaseCompile('com.crashlytics.sdk.android:crashlytics:2.6.0@aar') {
        transitive = true;
    }

    // View Elements Binding :)
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    //Dependency Injection
    compile 'com.google.dagger:dagger:2.7'
    apt 'com.google.dagger:dagger-compiler:2.7'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    //App Compatibility and Material Design
    compile('com.mikepenz:materialdrawer:5.3.6@aar') {
        transitive = true
    }
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.android.support:preference-v14:25.0.1'
    compile 'com.android.support:multidex:1.0.1'

    //QR Code
    compile 'com.github.kenglxn.QRGen:android:2.2.0'

    // Emojis
    compile 'com.github.viirus:emoji-lib:0.0.3.2'

    // Markdown
    compile 'com.commonsware.cwac:anddown:0.2.4'

    // About View for all dependent Libraries, we are using
    compile('com.mikepenz:aboutlibraries:5.7.1@aar') {
        transitive = true
    }

    // a better fab alternative
    compile 'com.github.clans:fab:1.6.4'

    // ORM
    apt 'com.raizlabs.android:DBFlow-Compiler:2.2.1'
    compile "com.raizlabs.android:DBFlow-Core:2.2.1"
    compile "com.raizlabs.android:DBFlow:2.2.1"

    //Eventbus
    compile 'org.greenrobot:eventbus:3.0.0'

    // IAP Handling / Verification
    compile 'org.solovyev.android:checkout:0.7.5@aar'

    //Facebook
    compile('com.facebook.android:facebook-android-sdk:4.17.0') {
        transitive = true
    }

    compile 'fr.avianey.com.viewpagerindicator:library:2.4.1@aar'

    //RxJava
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.0'

    //Analytics
    compile 'com.amplitude:android-sdk:2.9.2'

    // Fresco Image Management Library
    compile('com.facebook.fresco:fresco:0.14.0') {
        exclude module: 'bolts-android'
    }
    compile('com.facebook.fresco:animated-gif:0.14.0') {
        exclude module: 'bolts-android'
    }
    //Tests
    testCompile "junit:junit:4.10"
    testCompile "org.assertj:assertj-core:1.7.0"
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile "org.robolectric:robolectric:3.2"
    testCompile 'org.robolectric:shadows-multidex:3.1'
    testCompile "org.robolectric:shadows-support-v4:3.1"
    testCompile "org.mockito:mockito-core:1.10.19"

    //Leak Detection
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    //Push Notifications
    compile 'com.google.firebase:firebase-core:9.4.0'
    compile 'com.google.firebase:firebase-messaging:9.4.0'

    compile 'com.google.android.gms:play-services-auth:9.4.0'

    compile(project(':seeds-sdk')) {
        exclude group: 'com.google.android.gms'
        exclude group: 'com.android.support', module: 'multidex'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "com.habitrpg.android.habitica"
    }

    lintOptions {
        abortOnError false
    }
    signingConfigs {
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            multiDexEnabled true
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
            resValue "string", "content_provider", "com.habitrpg.android.habitica.debug.fileprovider"
            resValue "string", "app_name", "Habitica Debug"
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            multiDexEnabled true
            resValue "string", "content_provider", "com.habitrpg.android.habitica.fileprovider"
            resValue "string", "app_name", "Habitica"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/java']
            aidl.srcDirs = ['src/main/java']
            renderscript.srcDirs = ['src/main/java']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        robolectric {
            manifest.srcFile 'AndroidManifestTesting.xml'
            java.srcDir file('src/test/java/')
            res.srcDirs = ['res']
        }
        instrumentTest.setRoot('tests')
        release { java.srcDirs = ['src/release/java'] }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "4g" // Use gig increments depending on needs
    }

    lintOptions {
        disable 'UnusedResources'
    }
}

def Properties props = new Properties()
def propFile = new File('signingrelease.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

// Add Habitica Properties to buildConfigField
final File HRPG_PROPS_FILE = new File('habitica.properties')
if (HRPG_PROPS_FILE.canRead()) {
    def Properties HRPG_PROPS = new Properties()
    HRPG_PROPS.load(new FileInputStream(HRPG_PROPS_FILE))

    if (HRPG_PROPS != null) {
        android.buildTypes.all { buildType ->
            HRPG_PROPS.any { property ->
                buildType.buildConfigField "String", property.key, "\"${property.value}\""
            }
        }
    } else {
        throw new InvalidUserDataException('habitica.properties found but some entries are missing')
    }
} else {
    throw new MissingResourceException('habitica.properties not found')
}

// Add Habitica Resources to resources
final File HRPG_RES_FILE = new File('habitica.resources')
if (HRPG_RES_FILE.canRead()) {
    def Properties HRPG_RES = new Properties()
    HRPG_RES.load(new FileInputStream(HRPG_RES_FILE))

    if (HRPG_RES != null) {
        android.buildTypes.all { buildType ->
            HRPG_RES.any { property ->
                buildType.resValue "string", property.key, "\"${property.value}\""
            }
        }
    } else {
        throw new InvalidUserDataException('habitica.resources found but some entries are missing')
    }
} else {
    throw new MissingResourceException('habitica.resources not found')
}

tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}

apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.android.application' //or apply plugin: 'java'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.google.gms.google-services'